= Working with Hierarchical Trees in Neo4j
:author: Tomaz Bratanic
:description: Approach hierarchical tree structures in Neo4j by querying and exploring a hospital data set
:img: https://guides.neo4j.com/img
:tags: hierarchy, trees, parent-child

image:{img}/hospitalmeta.jpg[hospitalmeta,width=400]

== Introduction

My name is Tomaz Bratanic. I want to demonstrate how you should approach hierarchical location trees in Neo4j. From what I have learned during importing/querying with them, I came up with a few ground rules
one should follow to in order to get the correct query results.

=== Rules of location tree:

* _All relationships are directed from children to parents, going up the
hiearchy._
* _We have a single type for all relationships. (PARENT;FROM;IS_IN)_
* _Every node has a single outgoing relationship to its parent._
* _Every node can have one or multiple incoming relationships from its
children._

=== Contact:

* _twitter: @tb_tomaz_
* _github: https://github.com/tomasonjo_
* _blog: https://tbgraph.wordpress.com/category/hospital_

== Import

Let's load some data into our graph to explore.

=== Add constraints

First, we need to add indexes and constraints, as they will optimize your queries. The first array in the procedure below sets the indexes, and the second array contains the unique constraints.

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
CALL apoc.schema.assert(
 {County:['name'],City:['name'],ZipCode:['name'],Address:['name']},
 {Hospital:['id'],State:['name']});
----

== Load CSV

You can notice that we do not take the standard approach, where we
merge each node separately, but we merge them in pattern with their
parent in a hierarchical tree because some counties/cities/addresses share
the same name.

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
LOAD CSV WITH HEADERS FROM "https://raw.githubusercontent.com/tomasonjo/hospitals-neo4j/master/Hospital%20General%20Information.csv" as row
// state name is unique
MERGE (state:State{name:row.State})
// merge by pattern with their parents
MERGE (state)<-[:IS_IN]-(county:County{name:row.`County Name`})
MERGE (county)<-[:IS_IN]-(city:City{name:row.City})
MERGE (city)<-[:IS_IN]-(zip:ZipCode{name:row.`ZIP Code`})
MERGE (zip)<-[:IS_IN]-(address:Address{name:row.Address})
// for entities, it is best to have an id system
MERGE (h:Hospital{id:row.`Provider ID`})
ON CREATE SET h.phone=row.`Phone Number`,
              h.emergency_services = row.`Emergency Services`,
              h.name= row.`Hospital Name`,
              h.mortality = row.`Mortality national comparison`,
              h.safety = row.`Safety of care national comparison`,
              h.timeliness = row.`Timeliness of care national comparison`,
              h.experience = row.`Patient experience national comparison`,
              h.effectiveness = row.`Effectiveness of care national comparison`
MERGE (h)-[:IS_IN]->(address)
//Some metadata about hospitals
MERGE (type:HospitalType{name:row.`Hospital Type`})
MERGE (h)-[:HAS_TYPE]->(type)
MERGE (ownership:Ownership{name: row.`Hospital Ownership`})
MERGE (h)-[:HAS_OWNERSHIP]->(ownership)
MERGE (rating:Rating{name:row.`Hospital overall rating`})
MERGE (h)-[:HAS_RATING]->(rating)
----

== Geospatial import

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
LOAD CSV WITH HEADERS FROM "https://raw.githubusercontent.com/tomasonjo/hospitals-neo4j/master/gpsinfo.csv" as row
MATCH (hospital:Hospital {id:row.id})
SET hospital.latitude = row.latitude, hospital.longitude = row.longitude
----

== Spatial query example

Let's say you get lost on `Liberty Island` and want to find the nearest 10
hospitals. Distance is in meters. *Note: does not work in Neo4j Sandbox.*

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
with "1 Liberty Island, New York" as myLocation
call apoc.spatial.geocodeOnce(myLocation) yield location
WITH point({longitude: location.longitude, latitude: location.latitude}) as myPosition,100 as distanceInKm
MATCH (h:Hospital)-->(rating:Rating)
WHERE exists(h.latitude) and
distance(myPosition, point({longitude:h.longitude,latitude:h.latitude})) < (distanceInKm * 100)
RETURN h,rating.name as rating,distance(myPosition, point({longitude:h.longitude,latitude:h.latitude})) as distance order by distance limit 10
----

== Data Validation

=== Validation #1

We can check if any `:Address` has more than one relationship going up the hierarchy. Every node has a single outgoing relationship to its parent rule.

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
match (a:Address)
with a where size((a)-[:IS_IN]->()) > 1
return a
----

=== Validation #2

We can also check the length of all the paths in location tree.
Because of our rules we placed, every hospital must have exactly one
location path because every hospital have exactly one address.

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
MATCH path=(h:Hospital)-[:IS_IN*..10]->(location) where not (location)-[:IS_IN]->()
return distinct(length(path)) as length,count(*) as numberOfPaths,count(distinct(h)) as numberOfHospitals
----

=== Validation #3

Check how many labels each node has.
This is useful when learning. You do not wish to have nodes without labels.

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
match (n)
return size(labels(n)) as size,count(*)
----

== Queries

Lets run a few queries and learn about our data.

=== Average rating by ownership

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
MATCH (r)<-[:HAS_RATING]-(h:Hospital)-[:HAS_OWNERSHIP]->(o)
return o.name as ownership,avg(toINT(r.name)) as averageRating order by averageRating desc limit 15
----

=== Number of hospitals per city

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
MATCH (h:Hospital)-[:IS_IN*3..3]->(city)
return city.name as city,count(h) as NumberOfHospitals order by NumberOfHospitals desc limit 15
----

=== Top 10 states by rating

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
MATCH (r)<-[:HAS_RATING]-(h:Hospital)-[:IS_IN*5..5]->(state)
where not r.name="Not Available"
return state.name as state,avg(toINT(r.name)) as averageRating,count(h) as numberOfHospitals order by averageRating desc limit 15
----

=== Which states have the most above-average hospitals in effectivness

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
MATCH (h:Hospital)-[:IS_IN*5..5]->(state) where h.effectiveness = "Above the National average"
return state.name as state,h.effectiveness,count(h) as numberOfHospitals order by numberOfHospitals desc limit 15
----

=== Which states have the most below-average hospitals in mortality

[source,highlight,pre-scrollable,programlisting,cm-s-neo,code,runnable,standalone-example,ng-binding]
----
MATCH (h:Hospital)-[:IS_IN*5..5]->(state) where h.mortality = "Below the National average"
return state.name as state,h.mortality,count(h) as numberOfHospitals order by numberOfHospitals desc limit 15
----
